/*******************************************************************************
 * Copyright (c) 2019 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/

/**
 * Apply Jacoco plugin and configure 'jacoco', 'test', 'jacocoTestReport', and 'jacocoTestCoverageVerification' tasks.
 * See: https://docs.gradle.org/current/userguide/jacoco_plugin.html
 */
plugins.apply 'jacoco'

/**
 * Define dependencies for the Jacoco Ant plugin that is used to
 * generate coverage reports since Artifactory doesn't have the
 * POM and therefore can't resolve secondary dependencies.
 */
dependencies {
    jacocoAnt "org.jacoco:org.jacoco.agent:0.8.4",
            "org.jacoco:org.jacoco.ant:0.8.4",
            "org.jacoco:org.jacoco.core:0.8.4",
            "org.jacoco:org.jacoco.report:0.8.4",
            "org.ow2.asm:asm-all:5.2"
}

jacoco {
    toolVersion = "0.8.4"
}

test {
    /**
     * By default, don't generate code coverage unless we are
     * running the 'jacocoTestReport' task.
     */
    def enableJacoco = false
    gradle.startParameter.taskNames.each {
        if(it.contains("jacocoTestReport")) {
            enableJacoco = true;
        }
    }

    jacoco {
        enabled = enableJacoco
    }
}

jacocoTestReport {
    description = 'Run the test task and generate the code coverage report.'

    dependsOn test

    reports {
        html.enabled = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }
    }
}