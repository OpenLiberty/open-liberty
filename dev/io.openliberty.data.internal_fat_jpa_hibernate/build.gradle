/*******************************************************************************
 * Copyright (c) 2024 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License 2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-2.0/
 * 
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/
 // Always pull from Maven Central so we don't have to upload transitive dependencies to artifactory
apply from: '../wlp-gradle/subprojects/maven-central-mirror.gradle'

ext {
  hibernateVersion = '7.0.0.Alpha2' //TODO update to final
}

configurations {
  hibernateJPA32
}

dependencies {
  hibernateJPA32 "org.hibernate.orm:hibernate-core:${hibernateVersion}"

  //Annotation processor - generages .java files in build/libs/generated/sources
  //Some generated .java files import hibernate dependencies, need this compileOnly dependency
  annotationProcessor "org.hibernate.orm:hibernate-jpamodelgen:${hibernateVersion}"
  compileOnly "org.hibernate.orm:hibernate-core:${hibernateVersion}"
}

task addhibernateJPA32(type: Copy) {
  shouldRunAfter jar
  from configurations.hibernateJPA32
  into new File(autoFvtDir, 'publish/shared/resources/jpa32_hibernate')
}

// Copy bundle io.openliberty.org.testcontainers to AutoFVT/lib/
// This bundle is required both at compiletime and runtime.
addRequiredLibraries.dependsOn copyTestContainers

//TODO Hibernate does not support the current version of Derby we use for DatabaseRotation
//HHH000511: The 10.11.1 version for [org.hibernate.dialect.DerbyDialect] is no longer supported, hence certain features may not work properly.
//The minimum supported version is 10.15.2. Check the community dialects project for available legacy versions.
//Nothing is currently failing, but something might fail in the future.
addRequiredLibraries.dependsOn copyJdbcDrivers

addRequiredLibraries.dependsOn addhibernateJPA32

apply plugin: 'java'

compileJava {
  options.compilerArgs << '-parameters'
}