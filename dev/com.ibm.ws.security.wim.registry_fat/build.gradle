/*******************************************************************************
 * Copyright (c) 2017, 2022 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License 2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-2.0/
 * 
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/

dependencies { 
  requiredLibs project(':fattest.simplicity'),
    project(':com.ibm.ws.security.registry'),
    project(':com.ibm.ws.security.registry_test.custom'),
    project(':com.ibm.ws.security.registry_test.servlet'),
    'com.unboundid:unboundid-ldapsdk:5.1.0',
    project(':com.ibm.ws.com.unboundid')
}

autoFVT.dependsOn ':com.ibm.ws.security.registry_test.custom:assemble'
autoFVT.dependsOn ':com.ibm.ws.security.registry_test.servlet:assemble'
autoFVT.doLast {
  /*
   * Copy the local ApacheDS LDAP instances. 
   */
  copy {
    from project(':com.ibm.ws.org.apache.directory.server').projectDir
    into autoFvtDir
    include 'apacheds-2.0.0-M15/**'
  }

  /*
   * Copy the userRegistry WAR into test servers.
   */
  def servers = [
    'com.ibm.ws.security.wim.registry.fat.DefaultWIMRealm',
    'com.ibm.ws.security.wim.registry.fat.DefaultWIMRealmMultipleRepos',
    'com.ibm.ws.security.wim.registry.fat.WIMURRegression',
    'com.ibm.ws.security.wim.registry.fat.MultiValuedOutputPropMapping',
    'com.ibm.ws.security.wim.registry.fat.FederateStandaloneCur',
    'com.ibm.ws.security.wim.registry.fat.FederateStandaloneCurBell',
    'com.ibm.ws.security.wim.registry.fat.UserEnumeration'
  ]
  servers.each { server ->
    copy { 
      from new File(project(':com.ibm.ws.security.registry_test.servlet').buildDir, 'test-application')
      into new File(autoFvtDir, 'publish/servers/' + server + '/dropins')
      include '*.war'
    }
  }
  
  /*
   * Copy sample custom UserRegistry feature to this test bucket.
   */
  copy { 
    from project(':com.ibm.ws.security.registry_test.custom').file('publish/features/customRegistrySample-1.0.mf')
    into new File(autoFvtDir, 'lib/LibertyFATTestFiles/internalfeatures')
  }
  copy {
    from new File(project(':com.ibm.ws.security.registry_test.custom').buildDir, 'com.ibm.ws.security.registry_test.custom.jar')
    into new File(autoFvtDir, 'lib/LibertyFATTestFiles/internalfeatures')
    rename 'com.ibm.ws.security.registry_test.custom.jar', 'com.ibm.ws.security.registry_test.custom_1.0.jar'
  }
  
  /*
   * Copy sample CustomRepositoryDelay user feature to this test bucket.
   */
  copy { 
    from project(':com.ibm.ws.security.wim.repository_test.custom.delay').file('publish/usr/extension/lib/features/customRepositorySampleDelay-1.0.mf')
    into new File(autoFvtDir, 'publish/features')
  }
  copy {
    from new File(project(':com.ibm.ws.security.wim.repository_test.custom.delay').buildDir, 'com.ibm.ws.security.wim.repository_test.custom.delay.jar')
    into new File(autoFvtDir, 'publish/bundles')
    rename 'com.ibm.ws.security.wim.repository_test.custom.delay.jar', 'com.ibm.ws.security.wim.repository_test.custom.delay_1.0.jar'
  }
}
