/*******************************************************************************
 * Copyright (c) 2022, 2024 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License 2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-2.0/
 * 
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/
/* apply from: '../com.ibm.ws.security.oauth.oidc_fat.common/OAuthOidcCommon.gradle' */

repositories {
  if (isUsingArtifactory) {
    maven {
      credentials {
        username userProps.getProperty("artifactory.download.user")
        password userProps.getProperty("artifactory.download.token")
      }
      url ("https://" + gradle.userProps.getProperty("artifactory.download.server") + "/artifactory/wasliberty-maven-remote")
    }
  } else {
    mavenCentral()
  }
}

dependencies {
   requiredLibs project(':com.ibm.ws.security.fat.common'),
                project(':com.ibm.ws.security.fat.common.jwt'),
                project(':com.ibm.ws.security.fat.common.social'),
                project(':com.ibm.ws.security.fat.common.claimPropagation'),
                project(':com.ibm.ws.security.oauth.oidc_fat.common'),
                project(':com.ibm.ws.com.meterware.httpunit.1.7'),
                fileTree("${project(':com.ibm.ws.security.oauth.oidc_fat.common').buildDir}/collectedJars") { include '*.jar' },
                'jtidy:jtidy:4aug2000r7-dev',
                'rhino:js:1.6R5',
                project(':com.ibm.ws.org.apache.commons.compress'),
                project(':com.ibm.ws.com.unboundid'),
                'com.unboundid:unboundid-ldapsdk:5.1.0'
 
  // NOTE: More current js versions have a bug that prevents multiple redirects - this causes
  // a couple of our tests to fail.
                
  /*
   * Previously we had an uber jar named htmlunit-2.20-OSGi.jar. It appears to have contained all of
   * the runtime dependencies for htmlunit. Instead of generating an uber jar, we'll add the dependencies
   * here. The uber jar also was configured to be an OSGi bundle, but it is not necessary for this bucket.
   */ 
  requiredLibs project(':com.ibm.ws.org.apache.commons.io'),        // htmlunit-2.44.0 --> 2.8.0
               project(':com.ibm.ws.org.apache.commons.lang3'),     // htmlunit-2.44.0 --> 3.11
               'net.sourceforge.htmlunit:htmlunit-core-js:2.44.0',
               'net.sourceforge.htmlunit:htmlunit-cssparser:1.6.0',
               'net.sourceforge.htmlunit:neko-htmlunit:2.44.0',
               'org.apache.httpcomponents:httpmime:4.5.12',
               'org.brotli:dec:0.1.2',
               'xalan:xalan:2.7.2',
               project(':io.openliberty.org.apache.xercesImpl'),
               'httpunit:httpunit:1.7',
               'xml-apis:xml-apis:1.4.01'
}

addRequiredLibraries.dependsOn addJakartaTransformer

/*
 * This is where all application ZIP and WARs will be built.
 */
def appBuildDir = "${buildDir}/test-application"

/******************************************************************
 ******************************************************************
 **
 ** Copy feature bundles and applications to specific servers.
 **
 ******************************************************************
 ******************************************************************/

task copyOPServers (dependsOn: [':com.ibm.ws.security.fat.common:assemble', ':com.ibm.ws.security.fat.common.jwt:assemble', ':com.ibm.ws.security.fat.common.social:assemble', ':com.ibm.ws.security.oauth.oidc_fat.common:assemble', ':com.ibm.ws.security.fat.common.claimPropagation:assemble']) {
  doLast {

  /*
   * Copy the local ApacheDS LDAP instances.
   */
  copy {
    from project(':com.ibm.ws.org.apache.directory.server').projectDir
    into autoFvtDir
    include 'apacheds-2.0.0-M15/**'
  }

  /*
   * Copy common artifacts to test servers.
   */
  def servers = [
    'com.ibm.ws.security.claimPropagation_fat.op',
	'com.ibm.ws.security.claimPropagation_fat.op.external',
  ]

  servers.each { server ->

    copy { 
     from project(':com.ibm.ws.security.fat.common.claimPropagation').file("publish/servers/${server}")
     into new File(autoFvtDir, 'publish/servers/' + server)
     include '**'
    }

    copy { 
      from project(':com.ibm.ws.security.fat.common.claimPropagation').file('publish/files/serversettings/')
      into new File(autoFvtDir, 'publish/servers/' + server + '/imports')
      include '*.xml'
    }
    
    copy { 
      from new File(project(':com.ibm.ws.security.fat.common').buildDir, 'test-application/testmarker.war')
      into new File(autoFvtDir, 'publish/servers/' + server + '/dropins')
    }

    copy {
      from new File(project(':com.ibm.ws.security.oauth.oidc_fat.common').projectDir, "/securitykeys")
      into new File(autoFvtDir, 'publish/servers/' + server)
      include 'commonBasicKeyStore.jks'
      include 'commonTrustStore.jks'
    }
    
    copy {
      from new File(project(':com.ibm.ws.security.oauth.oidc_fat.common').projectDir, 'publish/files/serversettings/')
      into new File(autoFvtDir, 'publish/servers/' + server + '/imports')
      include 'formlogin_1.xml'
      include 'op_fatTestPorts.xml'
    }    
   
   }     
  } 
}
