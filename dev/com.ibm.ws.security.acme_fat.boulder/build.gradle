/*******************************************************************************
 * Copyright (c) 2019, 2020 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/

configurations {
    runBoulder { extendsFrom runtime }
}

dependencies {
  requiredLibs 'org.apache.commons:commons-compress:1.19',
               'org.rnorth.duct-tape:duct-tape:1.0.7',
               'org.rnorth.visible-assertions:visible-assertions:2.1.2',
               'org.rnorth:tcp-unix-socket-proxy:1.0.2',
               'net.java.dev.jna:jna:5.2.0',
               'org.testcontainers:testcontainers:1.14.0',
               'org.slf4j:slf4j-api:1.7.7',
               'org.slf4j:slf4j-jdk14:1.7.7',
               'org.apache.httpcomponents:httpclient:4.5.5',
               'org.apache.httpcomponents:httpcore:4.4.9',
               'commons-logging:commons-logging:1.1.1',
               'org.shredzone.acme4j:acme4j-client:2.7',
               'org.shredzone.acme4j:acme4j-utils:2.7',
               project(':com.ibm.ws.security.acme'),
               project(':com.ibm.ws.security.acme_fat'),
               project(':com.ibm.ws.crypto.certificateutil')
               

  runBoulder   'net.java.dev.jna:jna:5.2.0',
               'org.rnorth.visible-assertions:visible-assertions:2.1.2',
               'org.apache.commons:commons-compress:1.19',
               'commons-logging:commons-logging:1.1.1'
}

autoFVT.doLast {
  /*
   * Copy the CACERTS truststore(s) into all test servers.
   */
   new File("${projectDir}/publish/servers").eachDir { server ->
    copy { 
      from 'publish/files/'
      include 'cacerts.*'
      into new File(autoFvtDir, 'publish/servers/' + server.name + '/resources/security')
    }
  }
}

tasks.withType(JavaExec) {
    /* 
     * Assign all Java system properties from the command line to the JavaExec task.
     */
    systemProperties System.properties
}

task runBoulder(type: JavaExec) {
    group = "Execution"
    description = "Start a Boulder ACME CA server with a mock DNS server."
    
    workingDir = 'build/libs/autoFVT'
    classpath = files(sourceSets.main.runtimeClasspath, configurations.runBoulder)
    main = "com.ibm.ws.security.acme.docker.boulder.BoulderDriver"
}
