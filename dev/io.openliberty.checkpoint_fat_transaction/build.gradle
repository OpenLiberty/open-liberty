/*******************************************************************************
 * Copyright (c) 2023 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License 2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-2.0/
 * 
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/

task copyBundles {
  dependsOn jar
  enabled project.file('test-bundles').exists()
  doFirst {
    new File(project.buildDir, 'buildfiles').eachLine { String line ->
      if (!line.contains(project.name + '.jar')) {
        copy {
          from line
          into new File(autoFvtDir, 'publish/bundles')
        }
      }
    }
  }
}

configurations {
  derbyClient
}

task addDerbyClient(type: Copy) {
  shouldRunAfter jar
  from configurations.derbyClient
  into new File(autoFvtDir, 'publish/shared/resources/derbyclient')
  rename 'derbyclient.*.jar', 'derbyclient.jar'
}

task publishTxTestFeatures {
  dependsOn ':com.ibm.ws.transaction.test.util:publishFeatureBundles'
  dependsOn ':com.ibm.ws.transaction.test.util:publishFeatureManifests'
}

addRequiredLibraries {
  dependsOn addJakartaTransformer
  dependsOn addDerby
  dependsOn addDerbyClient
  dependsOn copyBundles
  dependsOn publishTxTestFeatures
}

dependencies {
  derbyClient 'org.apache.derby:derbyclient:10.11.1.1'
  requiredLibs 'org.apache.derby:derbynet:10.11.1.1',
               project(':com.ibm.ws.transaction.test.util'),
               project(':com.ibm.ws.transaction.fat.util'),
               project(':com.ibm.ws.tx.embeddable'),
               project(':io.openliberty.org.apache.commons.logging')
}
