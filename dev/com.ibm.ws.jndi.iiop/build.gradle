/* ***************************************************************************
 * Copyright (c) 2021 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 * ***************************************************************************/

// Set up an endorsed standards directory containing the relevant Yoko jars.
// We rely on the pattern matching to find all the relevant artifacts from the runtime classpath.
task stageEndorsed(type: Sync) {
    from configurations.runtimeClasspath { include "**/*yoko*jar" }
    into temporaryDir
}

test {
    // subtests must only run as part of containing suites
    filter { excludeTestsMatching "com.ibm.ws.jndi.iiop.subtests.*" }
    // pre Java 9: use the endorsed standards mechanism to override ORB API
    if (JavaVersion.current() < JavaVersion.VERSION_1_9) {
        dependsOn stageEndorsed                                      // populate endorsed dir
        jvmArgs "-Djava.endorsed.dirs=$stageEndorsed.destinationDir" // endorse endorsed dir
        doLast { delete stageEndorsed.destinationDir }               // clean up endorsed dir
    }
}
