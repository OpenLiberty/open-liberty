/*******************************************************************************
 * Copyright (c) 2021 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/

configurations {
  hibernateJPA21
  openjpaJPA21
  jpaFatTools
}

dependencies {
  jpaFatTools project(':com.ibm.ws.jpa_testframework')
  requiredLibs project(":com.ibm.ws.jpa.tests.beanvalidation_fat.common")

  hibernateJPA21 'org.hibernate:hibernate-core:5.2.18.Final'
  hibernateJPA21 'org.jboss.logging:jboss-logging:3.3.1.Final'
  hibernateJPA21 'org.javassist:javassist:3.25.0-GA'
  hibernateJPA21 'antlr:antlr:2.7.7'
  hibernateJPA21 'org.jboss:jandex:2.0.3.Final'
  hibernateJPA21 'com.fasterxml:classmate:1.3.0'
  hibernateJPA21 'org.hibernate.common:hibernate-commons-annotations:5.0.1.Final'
  hibernateJPA21 'org.dom4j:dom4j:2.1.1'

  openjpaJPA21   'org.apache.openjpa:openjpa:3.0.0'
  openjpaJPA21   'commons-collections:commons-collections:3.2.2'
  openjpaJPA21   'commons-dbcp:commons-dbcp:1.4'
  openjpaJPA21   'commons-logging:commons-logging:1.2'
  openjpaJPA21   'commons-pool:commons-pool:1.6'
  openjpaJPA21   'net.sourceforge.serp:serp:1.15.1'
  openjpaJPA21   'org.apache.xbean:xbean-asm6-shaded:4.8'
}

task addJPAFATTools (type: Copy) {
  mustRunAfter jar
  from configurations.jpaFatTools
  include "**/com.ibm.ws.jpa_testframework*.jar"
  into new File(autoFvtDir, 'lib')
  rename 'com.ibm.ws.jpa_testframework-(.*).jar', 'com.ibm.ws.jpa_testframework.jar'
}

task addhibernateJPA21(type: Copy) {
  shouldRunAfter jar
  from configurations.hibernateJPA21
  into new File(autoFvtDir, 'publish/shared/resources/jpa21_hibernate')
}

task addOpenJPA_JPA21(type: Copy) {
  shouldRunAfter jar
  from configurations.openjpaJPA21
  into new File(autoFvtDir, 'publish/shared/resources/jpa21_openjpa')
}

task copyFAT {
    dependsOn ':com.ibm.ws.jpa.tests.beanvalidation_fat.common:build'
    copy {
      from project(':com.ibm.ws.jpa.tests.beanvalidation_fat.common').file('fat/src/com/ibm/ws/jpa/tests/beanvalidation/tests')
      into new File('fat/src/com/ibm/ws/jpa/tests/beanvalidation/tests')
    }
}

task copyCommonFiles {
  dependsOn ':com.ibm.ws.jpa.tests.beanvalidation_fat.common:build'
  doLast {
    /*
     * Copy test application resources.
     */
    copy {
      from project(':com.ibm.ws.jpa.tests.beanvalidation_fat.common').file('test-applications')
      into new File('test-applications')
    }
    
    /*
     * Copy common publish files.
     */
    copy {
      from project(':com.ibm.ws.jpa.tests.beanvalidation_fat.common').file('publish')
      into new File('publish')
    }
  }
}

addRequiredLibraries {
  dependsOn copyJdbcDrivers
  dependsOn addhibernateJPA21
  dependsOn addOpenJPA_JPA21
  dependsOn addJakartaTransformer
  dependsOn addJPAFATTools
  dependsOn copyFAT
  dependsOn copyTestContainers
}

jar {
  dependsOn copyCommonFiles
}

autoFVT.dependsOn copyCommonFiles

clean.doLast {
  if (file('test-applications').exists()) {
    file('test-applications').deleteDir()
  }
  if (file('build').exists()) {
    file('build').deleteDir()
  }
  if (file('publish/servers').exists()) {
    file('publish/servers').deleteDir()
  }
  if (file('fat/src/com/ibm/ws/jpa/tests/beanvalidation/tests').exists()) {
    file('fat/src/com/ibm/ws/jpa/tests/beanvalidation/tests').deleteDir()
  }
}
