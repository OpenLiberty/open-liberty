/*******************************************************************************
 * Copyright (c) 2017 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/

import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'maven'

task copyPropertiesToBuildImage (type:Copy) {
    dependsOn jar
    from project.file('publish')
    into project.file('wlp/lib/versions')
    include '*.properties'
    filter(ReplaceTokens,
           tokens: [PRODUCT_VERSION: bnd.libertyRelease, PRODUCT_EDITION: bnd.productEdition, PRODUCT_LICENSE_TYPE: bnd.productLicenseType])
}

task copyReadmeToBuildImage (type:Copy) {
    dependsOn jar
    from project.file('publish')
    into project.file('wlp')
    include 'README.TXT'
    filter(ReplaceTokens,
           tokens: [BUILD_ID: bnd.buildID, LIBERTY_VERSION: bnd.libertyRelease])
}

task copyLicenseToBuildImage (type:Copy) {
    dependsOn jar
    from project.file('publish')
    into project.file('wlp')
    include 'LICENSE'
    include 'NOTICES'
    filter(ReplaceTokens,
           tokens: [BUILD_ID: bnd.buildID, LIBERTY_VERSION: bnd.libertyRelease])
}

task copySwidTagToBuildImage (type:Copy) {
    dependsOn jar
    from project.file('publish')
    into project.file('wlp/lib/versions/tags')
    include 'openliberty.io_OpenLiberty.swidtag'
    filter(ReplaceTokens,
           tokens: [ VERSION: bnd.libertyRelease ])
}

assemble {
    dependsOn copyPropertiesToBuildImage
    dependsOn copyReadmeToBuildImage
    dependsOn copyLicenseToBuildImage
    dependsOn copySwidTagToBuildImage
}

import groovy.json.*
import org.gradle.internal.os.OperatingSystem



File packageDir = new File(project.buildDir, 'openliberty')
File mavenDir = new File(project.buildDir, 'maven')
def props = new Properties()
rootProject.file('generated.properties').withInputStream { props.load(it) }

// Set the generated zip version to the product version and the
// version qualifier to be consistent
// with what is used for the individual Bundle-Versions
// as well as the server version output.
def releaseVersion = "${bnd.libertyRelease}-" + props.getProperty("version.qualifier")

class PackageLibertyWithFeatures extends DefaultTask {
    Closure withFeatures
    File outputTo

    @TaskAction
    void buildPackage() {
        // Create server in order to minify an image that only includes kind=ga features
        project.delete project.file('wlp/usr/servers/packageOpenLiberty')
        project.exec {
            workingDir project.file('wlp/bin')
            def args = ["create", "packageOpenLiberty"]
            if (OperatingSystem.current().windows) {
                commandLine = ["cmd", "/c", "server"] + args
            } else {
                commandLine = ["./server"] + args
            }
        }

        try {
            def serverXml = project.file("wlp/usr/servers/packageOpenLiberty/server.xml")
            serverXml.text = """<?xml version="1.0" encoding="UTF-8"?>
<server description="new server">
    <featureManager>${withFeatures()}
    </featureManager>
</server>
"""

            File archive = new File(project.buildDir, 'openliberty.zip')
            project.delete archive
            project.exec {
                workingDir project.file('wlp/bin')
                def args = ["package", "packageOpenLiberty", "--archive=${archive}", "--include=minify"]
                if (OperatingSystem.current().windows) {
                    commandLine = ["cmd", "/c", "server"] + args
                } else {
                    commandLine = ["./server"] + args
                }
            }

            // Postprocess to remove unwanted files
            project.delete outputTo
            project.copy {
                from project.zipTree(project.file(archive))
                exclude 'wlp/usr/servers/**'
                exclude 'wlp/lib/versions/package_*'
                into outputTo
            }

            //Now add the NOTICES and LICENSE files
            project.copy {
                from project.file('publish')
                include 'LICENSE'
                include 'NOTICES'
                into "$outputTo/wlp"
            }

        } catch (all) {
            println "-- EXCEPTION PACKAGING SERVER --"
            println all
            def messages = project.file("wlp/usr/servers/packageOpenLiberty/logs/messages.log")
            if (messages.exists()) {
                messages.eachLine { String line ->
                    println line
                }
            }
        }
    }
}

def gaPublicFeatures() {
    String features = ""
    gaFeatures(true).each {
        features += '<feature>' + it + '</feature>\n'
    }
    features
}

def webProfile8Features() {
    String features = project.file('profiles/webProfile8/features.xml').readLines()
    features
}

def javaee8Features() {
    String features = project.file('profiles/javaee8/features.xml').readLines()
    features
}

def microProfile2Features() {
    String features = project.file('profiles/microProfile2/features.xml').readLines()
    features
}

task packageOpenLiberty(type: PackageLibertyWithFeatures) {
    enabled props["ghe.build.type"] == null || !props["ghe.build.type"].contains("ifix")
    dependsOn parent.subprojects.assemble
    dependsOn ':com.ibm.websphere.appserver.features:publishFeatureResources'
    withFeatures this.&gaPublicFeatures
    outputTo packageDir
}

task packageOpenLibertyKernel(type: PackageLibertyWithFeatures) {
    enabled props["ghe.build.type"] == null || !props["ghe.build.type"].contains("ifix")
    dependsOn parent.subprojects.assemble
    dependsOn ':com.ibm.websphere.appserver.features:publishFeatureResources'
    withFeatures { '' }
    outputTo packageDir
}

def removeFeature(feature) {
    def file = new File("$projectDir/wlp/lib/features/com.ibm.websphere.appserver." + feature + ".mf")
    file.renameTo(new File("$projectDir/wlp/lib/features/com.ibm.websphere.appserver." + feature + ".mf.bak"))
}

def restoreFeature(feature) {
    def file = new File("$projectDir/wlp/lib/features/com.ibm.websphere.appserver." + feature + ".mf.bak")
    file.renameTo(new File("$projectDir/wlp/lib/features/com.ibm.websphere.appserver." + feature + ".mf"))
}

def excludedFeatures = ['cdi-1.2',
                        'jsonp-1.0',
                        'jaxrs-2.0',
                        'jdbc-4.0',
                        'jdbc-4.1',
                        'jaxrsClient-2.0',
                        'servlet-3.1']

task packageOpenLibertyWebProfile8(type: PackageLibertyWithFeatures) {
    doFirst {
        excludedFeatures.each {
            this.&removeFeature("${it}")
        }
    }
    enabled props["ghe.build.type"] == null || !props["ghe.build.type"].contains("ifix")
    dependsOn parent.subprojects.assemble
    dependsOn ':com.ibm.websphere.appserver.features:publishFeatureResources'
    withFeatures this.&webProfile8Features
    outputTo packageDir
    doLast {
        copy {
            from "$packageDir/wlp/templates/servers/webProfile8/server.xml" 
            into "$packageDir/wlp/templates/servers/defaultServer" 
        }
        copy {
            from "$packageDir/wlp/templates/clients/javaeeClient8/client.xml"
            into "$packageDir/wlp/templates/clients/defaultClient" 
        }
        excludedFeatures.each {
            this.&restoreFeature("${it}")
        }
    }
}

task packageOpenLibertyJavaee8(type: PackageLibertyWithFeatures) {
    doFirst {
        excludedFeatures.each {
            this.&removeFeature("${it}")
        }
    }

    enabled props["ghe.build.type"] == null || !props["ghe.build.type"].contains("ifix")
    dependsOn parent.subprojects.assemble
    dependsOn ':com.ibm.websphere.appserver.features:publishFeatureResources'
    withFeatures this.&javaee8Features
    outputTo packageDir
    doLast {
        copy {
            from "$packageDir/wlp/templates/servers/javaee8/server.xml" 
            into "$packageDir/wlp/templates/servers/defaultServer" 
        }
        copy {
            from "$packageDir/wlp/templates/clients/javaeeClient8/client.xml"
            into "$packageDir/wlp/templates/clients/defaultClient" 
        }
        excludedFeatures.each {
            this.&restoreFeature("${it}")
        }
    }
}

task packageOpenLibertyMicroProfile2(type: PackageLibertyWithFeatures) {
    doFirst {
        excludedFeatures.each {
            this.&removeFeature("${it}")
        }
    }

    enabled props["ghe.build.type"] == null || !props["ghe.build.type"].contains("ifix")
    dependsOn parent.subprojects.assemble
    dependsOn ':com.ibm.websphere.appserver.features:publishFeatureResources'
    withFeatures this.&microProfile2Features
    outputTo packageDir
    doLast {
        copy {
            from "$packageDir/wlp/templates/servers/microProfile2/server.xml" 
            into "$packageDir/wlp/templates/servers/defaultServer" 
        }
        excludedFeatures.each {
            this.&restoreFeature("${it}")
        }
    }
}

// Includes only kind=ga features.
task zipOpenLiberty(type: Zip) {
    dependsOn packageOpenLiberty
    baseName 'openliberty'
    from packageDir
    destinationDir distsDir
    version releaseVersion
    doLast{
        props.setProperty('zipopenliberty.runtime.archivename', archivePath.toString())
        File propsFile = rootProject.file('generated.properties')
        props.store(propsFile.newWriter(), null)
    }
}
publish.dependsOn zipOpenLiberty

// Includes only kind=ga features.
task zipOpenLibertyKernel(type: Zip) {
    dependsOn packageOpenLibertyKernel
    baseName 'openliberty-kernel'
    from packageDir
    destinationDir distsDir
    version releaseVersion
    doLast{
        props.setProperty('zipopenliberty.kernel.archivename', archivePath.toString())
        File propsFile = rootProject.file('generated.properties')
        props.store(propsFile.newWriter(), null)
    }
}
publish.dependsOn zipOpenLibertyKernel

// Includes only kind=ga features.
task zipOpenLibertyWebProfile8(type: Zip) {
    dependsOn packageOpenLibertyWebProfile8
    baseName 'openliberty-webProfile8'
    from packageDir
    destinationDir distsDir
    version releaseVersion
    doLast{
        props.setProperty('zipopenliberty.webprofile8.archivename', archivePath.toString())
        File propsFile = rootProject.file('generated.properties')
        props.store(propsFile.newWriter(), null)
    }
}
publish.dependsOn zipOpenLibertyWebProfile8

// Includes only kind=ga features.
task zipOpenLibertyJavaee8(type: Zip) {
    dependsOn packageOpenLibertyJavaee8
    baseName 'openliberty-javaee8'
    from packageDir
    destinationDir distsDir
    version releaseVersion
    doLast{
        props.setProperty('zipopenliberty.javaee8.archivename', archivePath.toString())
        File propsFile = rootProject.file('generated.properties')
        props.store(propsFile.newWriter(), null)
    }
}
publish.dependsOn zipOpenLibertyJavaee8

task zipOpenLibertyMicroProfile2(type: Zip) {
    dependsOn packageOpenLibertyMicroProfile2
    baseName 'openliberty-microProfile2'
    from packageDir
    destinationDir distsDir
    version releaseVersion
    doLast {
        props.setProperty('zipopenliberty.microprofile2.archivename', archivePath.toString())
        File propsFile = rootProject.file('generated.properties')
        props.store(propsFile.newWriter(), null)
    }
}
publish.dependsOn zipOpenLibertyMicroProfile2

// Includes all features.
task zipOpenLibertyAll(type: Zip) {
    dependsOn parent.subprojects.assemble
    dependsOn ':com.ibm.websphere.appserver.features:publishFeatureResources'
    baseName 'openliberty-all'
    from projectDir
    include 'wlp/**'
    destinationDir distsDir
    version releaseVersion
    doLast {
        props.setProperty('zipopenliberty.archivename', archivePath.toString())
        File propsFile = rootProject.file('generated.properties')
        props.store(propsFile.newWriter(), null)
    }
}
publish.dependsOn zipOpenLibertyAll


clean.doLast {
    file('wlp').deleteDir()
    file('build').deleteDir()
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'openliberty'
            version project.version
            artifact zipOpenLibertyAll
        }
        openLiberty(MavenPublication) {
            groupId 'io.openliberty'
            artifactId 'openliberty-runtime'
            version project.version
            artifact zipOpenLiberty
        }
        openLibertyKernel(MavenPublication) {
            groupId 'io.openliberty'
            artifactId 'openliberty-kernel'
            version project.version
            artifact zipOpenLibertyKernel
        }
        openLibertyWebProfile8(MavenPublication) {
            groupId 'io.openliberty'
            artifactId 'openliberty-webProfile8'
            version project.version
            artifact zipOpenLibertyWebProfile8
        }
        openLibertyJavaee8(MavenPublication) {
            groupId 'io.openliberty'
            artifactId 'openliberty-javaee8'
            version project.version
            artifact zipOpenLibertyJavaee8
        }
    }
}

task createOLRuntimePoms {
    dependsOn packageOpenLiberty
    pom {
        project {
            groupId 'io.openliberty'
            artifactId 'openliberty-runtime'
            version bnd.libertyRelease
            packaging 'zip'
            name 'Open Liberty Runtime Package'
            description 'Open Liberty Runtime Package'
            url 'https://openliberty.io/'
            licenses {
                license {
                    name 'Eclipse Public License v1.0'
                    url 'http://www.eclipse.org/legal/epl-v10.html'
                    distribution 'repo'
                }
            }
            scm {
                connection 'scm:git:git@github.com:OpenLiberty/open-liberty.git'
                developerConnection 'scm:git:git@github.com:OpenLiberty/open-liberty.git'
                url 'git@github.com:OpenLiberty/open-liberty.git'
                tag 'HEAD'
            }
            developers {
                developer {
                    id 'cbridgha'
                    name 'Chuck Bridgham'
                    email 'cbridgha@us.ibm.com'
                }
            }
        }
    }.writeTo("${mavenDir}/openliberty-runtime-${bnd.libertyRelease}.pom")
    pom {
        project {
            groupId 'io.openliberty'
            artifactId 'openliberty-webProfile8'
            version bnd.libertyRelease
            packaging 'zip'
            name 'Open Liberty JavaEE 8 Web Profile Package'
            description 'Open Liberty JavaEE 8 Web Profile Package'
            url 'https://openliberty.io/'
            licenses {
                license {
                    name 'Eclipse Public License v1.0'
                    url 'http://www.eclipse.org/legal/epl-v10.html'
                    distribution 'repo'
                }
            }
            scm {
                connection 'scm:git:git@github.com:OpenLiberty/open-liberty.git'
                developerConnection 'scm:git:git@github.com:OpenLiberty/open-liberty.git'
                url 'git@github.com:OpenLiberty/open-liberty.git'
                tag 'HEAD'
            }
            developers {
                developer {
                    id 'cbridgha'
                    name 'Chuck Bridgham'
                    email 'cbridgha@us.ibm.com'
                }
            }
        }
    }.writeTo("${mavenDir}/openliberty-webProfile8-${bnd.libertyRelease}.pom")
    pom {
        project {
            groupId 'io.openliberty'
            artifactId 'openliberty-kernel'
            version bnd.libertyRelease
            packaging 'zip'
            name 'Open Liberty Kernel Package'
            description 'Open Liberty Kernel Package'
            url 'https://openliberty.io/'
            licenses {
                license {
                    name 'Eclipse Public License v1.0'
                    url 'http://www.eclipse.org/legal/epl-v10.html'
                    distribution 'repo'
                }
            }
            scm {
                connection 'scm:git:git@github.com:OpenLiberty/open-liberty.git'
                developerConnection 'scm:git:git@github.com:OpenLiberty/open-liberty.git'
                url 'git@github.com:OpenLiberty/open-liberty.git'
                tag 'HEAD'
            }
            developers {
                developer {
                    id 'cbridgha'
                    name 'Chuck Bridgham'
                    email 'cbridgha@us.ibm.com'
                }
            }
        }
    }.writeTo("${mavenDir}/openliberty-kernel-${bnd.libertyRelease}.pom")
    pom {
        project {
            groupId 'io.openliberty'
            artifactId 'openliberty-javaee8'
            version bnd.libertyRelease
            packaging 'zip'
            name 'Open Liberty JavaEE8 Package'
            description 'Open Liberty JavaEE8 Package'
            url 'https://openliberty.io/'
            licenses {
                license {
                    name 'Eclipse Public License v1.0'
                    url 'http://www.eclipse.org/legal/epl-v10.html'
                    distribution 'repo'
                }
            }
            scm {
                connection 'scm:git:git@github.com:OpenLiberty/open-liberty.git'
                developerConnection 'scm:git:git@github.com:OpenLiberty/open-liberty.git'
                url 'git@github.com:OpenLiberty/open-liberty.git'
                tag 'HEAD'
            }
            developers {
                developer {
                    id 'cbridgha'
                    name 'Chuck Bridgham'
                    email 'cbridgha@us.ibm.com'
                }
            }
        }
    }.writeTo("${mavenDir}/openliberty-javaee8-${bnd.libertyRelease}.pom")
}


task zipOpenLibertyMaven(type: Zip) {
    dependsOn createOLRuntimePoms
    archiveName "openliberty-runtimes-maven-${releaseVersion}.zip"
    from mavenDir
    from distsDir
    include '*.zip'
    include '*.pom'
    exclude 'openliberty-all*.zip'
    destinationDir project.file('build/temp/mavenArtifact')
}
if (isRelease) {
    publish.dependsOn zipOpenLibertyMaven
}




task createJSONForPublicArtifacts {
    enabled isAutomatedBuild

    doLast {
        delete "${buildDir}/info.json"
        File json = new File("${buildDir}/info.json")
        json.createNewFile()

        String testsPassed = "0"
        if (props.getProperty("tests.total.successful") != null) {
            testsPassed = props.getProperty("tests.total.successful")
        }
        String testsTotal = "0"
        if ((props.getProperty("tests.total.successful") != null) &&
                (props.getProperty("tests.total.failed") != null)) {
            def sum = Integer.valueOf(props.getProperty("tests.total.successful")) + Integer.valueOf(props.getProperty("tests.total.failed"))
            testsTotal = sum.toString()
        }

        String junitPath = props.getProperty("junit.report.zip")
        String testReportPath = props.getProperty("gradle.test.report.zip");
        String logPath = props.getProperty("published.gradle.log")
        String driverLocation = props.getProperty("zipopenliberty.archivename")

        if (junitPath == null) {
            raise InvalidUserDataException("Could not find property named 'junit.report.zip'")
        }

        if (isPersonal && (testReportPath == null)) {
            raise InvalidUserDataException("Could not find property named 'gradle.test.report.zip'")
        }

        if (logPath == null) {
            raise InvalidUserDataException("Could not find property named 'published.gradle.log'")
        }

        if (driverLocation == null) {
            raise InvalidUserDataException("Could not find property named 'zipopenliberty.archivename'")
        }

        String testResultName = null
        if (isPersonal) {
            testResultName = new File(testReportPath).getName()
        } else {
            testResultName = new File(junitPath).getName()
        }
        String logName = new File(logPath).getName()
        String driverLocationName = new File(driverLocation).getName()

        def object = [test_passed: "${testsPassed}",
                      total_tests: "${testsTotal}",
                      tests_log: "${testResultName}",
                      build_log: "${logName}"]

        if (isPersonal || isRelease || isContinuousBuild) {
            object.driver_location = "${driverLocationName}"
            object.version = "${project.version}"
        }

        json.text = JsonOutput.prettyPrint(JsonOutput.toJson(object))

        props.setProperty('published.json', json.toString())

        File propsFile = new File('generated.properties')
        props.store(propsFile.newWriter(), null)
    }
}

buildscript {
    repositories {
        if (isUsingArtifactory) {
            maven {
                credentials {
                    username userProps.getProperty("artifactory.download.user")
                    password userProps.getProperty("artifactory.download.token")
                }
                url ("https://" + userProps.getProperty("artifactory.download.server") + "/artifactory/wasliberty-open-liberty")
            }
        } else {
            jcenter()
        }
    }

    dependencies {
        classpath 'org.hidetake:gradle-ssh-plugin:2.9.0'
        classpath 'org.hidetake:groovy-ssh:2.9.0'
        classpath 'com.jcraft:jsch:0.1.54'
    }
}

apply plugin: 'org.hidetake.ssh'

remotes {
    dhe {
        host = props.getProperty("dhe.server")
        user = props.getProperty("intranet.user")
        password = props.getProperty("intranet.password")
        knownHosts = allowAnyHosts
    }
}

ssh.settings {
    jschLog = true
    logging = "stdout"
    fileTransfer = "scp"
    retryCount = 3
    retryWaitSec = 5
}

task publishPublicArtifactsOnDhe {
    dependsOn createJSONForPublicArtifacts
    enabled isAutomatedBuild && isPublicPublishing && (isPersonal || isContinuousBuild || isRelease)

    doLast {
        // Re-arrange buildLabel, ex: 201708101600 to 2017-08-10_1600
        def (yr, mo, d, t) = [bnd.buildLabel.substring(0,4), bnd.buildLabel.substring(4,6), bnd.buildLabel.substring(6,8), bnd.buildLabel.substring(8)]
        String version = "${yr}-${mo}-${d}_${t}"
        String packageName = null
        if (isRelease) {
            packageName = props.getProperty("published.package.nightly")
        } else {
            packageName = props.getProperty("published.package.personal")
            version = props.getProperty("buildResultUUID")
        }

        String userAtHost = props.getProperty("intranet.user") + "@" + props.getProperty("dhe.server")
        String dir = "/www/stage/export/pub/software/openliberty/runtime"
        String publicDHEUrl = "http://public.dhe.ibm.com/ibmdl/export/pub/software/openliberty/runtime"
        String dest = userAtHost + ":" + dir

        def artifactList = [props.getProperty("published.json"),
                            props.getProperty("published.gradle.log")]

        artifactList.add(props.getProperty("zipopenliberty.archivename"))

        if (isPersonal) {
            artifactList.add(props.getProperty("gradle.test.report.zip"))
        } else {
            artifactList.add(props.getProperty("junit.report.zip"))
            // add runtime, javaee8, and webprofile8 images to publish list for release builds only
            if (isRelease) {
                artifactList.add(props.getProperty("zipopenliberty.runtime.archivename"))
                artifactList.add(props.getProperty("zipopenliberty.javaee8.archivename"))
                artifactList.add(props.getProperty("zipopenliberty.webprofile8.archivename"))
            }
        }

        mkdir("${buildDir}/${version}")
        artifactList.each { filePath ->
            copy {
                from filePath
                into "${buildDir}/${version}"
                println "Copied $filePath to ${buildDir}/${version}" 
                print "isUnittestsDisabled = "
                print isUnittestsDisabled
				println()
                if (!isUnittestsDisabled && props.getProperty("gradle.test.report.zip") != null && props.getProperty("gradle.test.report.zip").equals(filePath)) {
                    def reportZip = file(props.getProperty("gradle.test.report.zip"))
                    from zipTree(reportZip)
                    mkdir("${buildDir}/${version}/junit")
                    into "${buildDir}/${version}/junit"
                }
            }
        }

        // Upload build artifacts
        println "Uploading build artifacts"
        ssh.run {
            session(remotes.dhe) {
                put from: "${buildDir}/${version}", into: "${dir}/${packageName}/"
            }
        }
        println "Completed upload of artifacts"

        delete "${buildDir}/${version}"
        delete "${buildDir}/info.json"

        // Update build listing json with the newly uploaded build
        println "Updating build listing json with newly uploaded build"
        exec {
            commandLine "curl", "-o", "${buildDir}/info.json", "${publicDHEUrl}/${packageName}/info.json"
        }

        File json = new File("${buildDir}/info.json")
        if (json.getText().contains("404 Not Found")) {
            json.text = ""
        }

        def object = [:]
        if (json.getText() != "") {
            object = new JsonSlurper().parseText(json.getText())
        }

        List versionsList = object['versions']
        if (versionsList != null) {
            versionsList.add("${version}")
        } else {
            versionsList = ["${version}"]
        }
        object['versions'] = versionsList
        json.text = JsonOutput.toJson(object)

        println "Uploading info.json"
        ssh.run {
            session(remotes.dhe) {
                put from: "${buildDir}/info.json", into: "${dir}/${packageName}/"
            }
        }
        println "Completed info.json upload"
    }
}
