/*******************************************************************************
 * Copyright (c) 2019, 2024 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License 2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-2.0/
 * 
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/
apply from: '../wlp-gradle/subprojects/maven-central-mirror.gradle'

// Use a variable for the aws sdk version so that we can test customer issues easily
ext.awsSDKVerion = '2.20.127'

configurations {
  aws
  postgres
}

dependencies {
  aws      "software.amazon.jdbc:aws-advanced-jdbc-wrapper:2.2.3",
           "software.amazon.awssdk:secretsmanager:${awsSDKVerion}", // Used for Secrets Manager
           "software.amazon.awssdk:rds:${awsSDKVerion}", // Used for Identity and Access Management
           project(':io.openliberty.com.fasterxml.jackson')
  postgres 'org.postgresql:postgresql:42.7.2'
}

task copyAWS(type: Copy) {
  shouldRunAfter jar
  from configurations.aws
  into new File(autoFvtDir, "publish/shared/resources/aws/")
  rename "aws-advanced-jdbc-wrapper-.*.", "aws-advanced-jdbc-wrapper.jar"
}

task copyPostgreSQLAWS(type: Copy) {
  shouldRunAfter jar
  from configurations.postgres
  into new File(autoFvtDir, "publish/shared/resources/aws/")
  rename "postgresql-.*.", "postgresql.jar"
}

task copySharedPostgres(type: Copy) {
  mustRunAfter jar
  from configurations.postgres
  into new File(autoFvtDir, 'publish/shared/resources/postgres')
  rename 'postgresql-.*.jar', 'postgresql.jar'
}

task copyAnonymousPostgres(type: Copy) {
  mustRunAfter jar
  from configurations.postgres
  into new File(autoFvtDir, 'publish/servers/postgresql-test-server/jdbc')
  rename 'postgresql-.*.jar', 'driver.jar'
}

addRequiredLibraries {
  dependsOn copyAWS
  dependsOn copyPostgreSQLAWS
  dependsOn copySharedPostgres
  dependsOn copyAnonymousPostgres
  dependsOn copyTestContainers
}
