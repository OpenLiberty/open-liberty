/*******************************************************************************
 * Copyright (c) 2019, 2020 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/
/* apply from: '../gradle/subprojects/fat.gradle' */

dependencies {
  requiredLibs project(':com.ibm.ws.security.fat.common.jwt'),
	project(':com.ibm.ws.org.jose4j'),
     project(':com.ibm.ws.com.unboundid'),
	  project(':io.openliberty.org.apache.commons.logging'),
      project(':com.ibm.ws.org.apache.httpcomponents'),
      'net.sourceforge.htmlunit:htmlunit:2.44.0',
      'net.sourceforge.htmlunit:neko-htmlunit:2.44.0',
      'net.sourceforge.htmlunit:webdriver:2.6',
      'org.seleniumhq.webdriver:webdriver-common:0.9.7376',
      'org.brotli:dec:0.1.2',
      'net.sourceforge.htmlunit:htmlunit-core-js:2.44.0',
      'net.sourceforge.htmlunit:htmlunit-cssparser:1.6.0',
      'org.apache.commons:commons-lang3:3.8',
      'org.eclipse.jetty:jetty-websocket:8.2.0.v20160908',
      'org.eclipse.jetty.websocket:websocket-api:9.4.5.v20170502',
      'org.eclipse.jetty.websocket:websocket-common:9.4.5.v20170502',
      'org.eclipse.jetty.websocket:websocket-client:9.4.5.v20170502',
      'org.eclipse.jetty:jetty-util:9.4.7.RC0',
      'org.eclipse.birt.runtime:org.w3c.css.sac:1.3.1.v200903091627',
      project(':io.openliberty.org.apache.commons.codec'),
      'xml-apis:xml-apis:1.4.01',
      'xerces:xercesImpl:2.11.0',
      'xalan:xalan:2.7.2',
      project(':com.ibm.ws.org.apache.commons.io'),
      'org.slf4j:slf4j-api:1.7.7',
      'com.unboundid:unboundid-ldapsdk:4.0.9'
      
}

addRequiredLibraries.dependsOn addJakartaTransformer

/*
 * This is where all application ZIP and WARs will be built.
 */
def appBuildDir = "${buildDir}/test-applications"

task jwtbuilderclient(type: War) {
  dependsOn classes
  dependsOn ':com.ibm.ws.security.fat.common:assemble'
  dependsOn ':com.ibm.ws.security.fat.common.jwt:assemble'

  def appName = "jwtbuilderclient"
  destinationDir file("${appBuildDir}")
  archiveName "${appName}.war"

  from("test-applications/${appName}/resources") {
    include "**/*"
  }
  from ("build/classes/java/main") {
    include "com/ibm/ws/jaxrs/fat/${appName}/*.class"    
    into "WEB-INF/classes"
  }
  from ("build/classes/java/main") {
    include "com/ibm/ws/security/jwt/fat/builder/*.class"    
    into "WEB-INF/classes"
  }
  /* copy needed files from jwt common fat into our app */
  from(zipTree("../com.ibm.ws.security.fat.common/build/libs/com.ibm.ws.security.fat.common.jar")) {
    include 'com/ibm/ws/security/fat/common/Constants.class', 'com/ibm/ws/security/fat/common/utils/KeyTools.class'
    into "WEB-INF/classes"
  }
  /* copy needed files from jwt common fat into our app */
  from(zipTree("../com.ibm.ws.security.fat.common.jwt/build/libs/com.ibm.ws.security.fat.common.jwt.jar")) {
    include 'com/ibm/ws/security/fat/common/jwt/JwtConstants.class', 'com/ibm/ws/security/fat/common/jwt/utils/JWTApiApplicationUtils.class'
    into "WEB-INF/classes"
  }  
   /* start of jose4j copy */  
  /* copy contents of the jose4j jar into our test app - we don't have another way to get to the jar */
  from(zipTree("../com.ibm.ws.org.jose4j/build/libs/com.ibm.ws.org.jose4j.jar")) {
    into "WEB-INF/classes"
  }
  /* end of jose4j copy */    
}

assemble {
  dependsOn jwtbuilderclient
}

autoFVT.dependsOn ':com.ibm.ws.security.fat.common:assemble'
autoFVT.dependsOn ':com.ibm.ws.security.fat.common.jwt:assemble'

autoFVT.doLast {

  def allServers = [
        'com.ibm.ws.security.jwt_fat.builder',
        'com.ibm.ws.security.jwt_fat.builder.rs'
  ]

  def builderServers = [
        'com.ibm.ws.security.jwt_fat.builder'
  ]
  
  def rsServers = [
        'com.ibm.ws.security.jwt_fat.builder.rs'
  ]
  
  allServers.each { server ->

    /* copy test marker (small test app that marks the start/end of each test case in the server side logs */
    copy {
      from new File(project(':com.ibm.ws.security.fat.common').buildDir, 'test-application/testmarker.war')
      into new File(autoFvtDir, 'publish/servers/' + server + '/dropins')
    }
    
   	/* Copy all shared key/trust stores from the common security FAT */
    copy { 
     from project(':com.ibm.ws.security.fat.common').file('publish/shared/securityKeys')
     into new File(autoFvtDir, 'publish/servers/' + server + '/')
    }
    /* Copy all shared key/trust stores from the common security JWT FAT */
    copy { 
     from project(':com.ibm.ws.security.fat.common.jwt').file('publish/shared/securityKeys')
     into new File(autoFvtDir, 'publish/servers/' + server + '/')
    }
  	/* Copy all shared key/trust stores from this project
  		overwrites shared jks files from the common security FAT 
  	*/
    copy { 
     from ('publish/shared/securityKeys')
     into new File(autoFvtDir, 'publish/servers/' + server + '/')
    }
  	/* Copy all server specific key/trust stores - overwrites shared
  		key/trust stores from either the common security FAT, or this
  		project
  	 */
    copy { 
     from new File(autoFvtDir, 'publish/servers/' + server + '/securityKeys')
     into new File(autoFvtDir, 'publish/servers/' + server + '/')
    }
  }
  
    builderServers.each { server ->
     copy {
      from new File(appBuildDir, 'jwtbuilderclient.war')
      into new File(autoFvtDir, 'publish/servers/' + server + '/test-apps')
    }
    copy { 
      from new File(project(':com.ibm.ws.security.fat.common.jwt').buildDir, 'test-application/jwtbuilder.war')
      into new File(autoFvtDir, 'publish/servers/' + server + '/test-apps')
    }
    
        /* copy the all signature alg config file from security_fat.common */
    copy {
      from project(':com.ibm.ws.security.fat.common').file('publish/shared/config')
      into new File(autoFvtDir, 'publish/shared/config')
    }
    
   }
    
    rsServers.each { server ->
    copy { 
      from new File(project(':com.ibm.ws.security.fat.common.jwt').buildDir, 'test-application/helloworld.war')
      into new File(autoFvtDir, 'publish/servers/' + server + '/test-apps')
    }
   }
} 
  

