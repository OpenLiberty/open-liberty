/*******************************************************************************
 * Copyright (c) 2021 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/

configurations {
  hibernateJPA22
  openjpaJPA22
  jpaFatTools
}

dependencies {
  jpaFatTools project(':com.ibm.ws.jpa_testframework')
  requiredLibs project(":com.ibm.ws.jpa.tests.beanvalidation_fat.common")

  hibernateJPA22 'org.hibernate:hibernate-core:5.5.7.Final'
  hibernateJPA22 'org.jboss.logging:jboss-logging:3.4.2.Final'
  hibernateJPA22 'org.javassist:javassist:3.27.0-GA'
  hibernateJPA22 'antlr:antlr:2.7.7'
  hibernateJPA22 'org.jboss:jandex:2.2.3.Final'
  hibernateJPA22 'com.fasterxml:classmate:1.5.1'
  hibernateJPA22 'org.hibernate.common:hibernate-commons-annotations:5.1.2.Final'
  hibernateJPA22 'net.bytebuddy:byte-buddy:1.11.12'

  openjpaJPA22   'org.apache.openjpa:openjpa:3.1.2'
  openjpaJPA22   'org.apache.commons:commons-collections4:4.4'
  openjpaJPA22   'org.apache.commons:commons-dbcp2:2.7.0'
  openjpaJPA22   'commons-logging:commons-logging:1.2'
  openjpaJPA22   'org.apache.commons:commons-pool2:2.6.0'
  openjpaJPA22   'net.sourceforge.serp:serp:1.15.1'
  openjpaJPA22   'org.apache.xbean:xbean-asm8-shaded:4.17'
}

task addJPAFATTools (type: Copy) {
  mustRunAfter jar
  from configurations.jpaFatTools
  include "**/com.ibm.ws.jpa_testframework*.jar"
  into new File(autoFvtDir, 'lib')
  rename 'com.ibm.ws.jpa_testframework-(.*).jar', 'com.ibm.ws.jpa_testframework.jar'
}

task addhibernateJPA22(type: Copy) {
  shouldRunAfter jar
  from configurations.hibernateJPA22
  into new File(autoFvtDir, 'publish/shared/resources/jpa22_hibernate')
}

task addOpenJPA_JPA22(type: Copy) {
  shouldRunAfter jar
  from configurations.openjpaJPA22
  into new File(autoFvtDir, 'publish/shared/resources/jpa22_openjpa')
}

task copyFAT {
    dependsOn ':com.ibm.ws.jpa.tests.beanvalidation_fat.common:build'
    copy {
      from project(':com.ibm.ws.jpa.tests.beanvalidation_fat.common').file('fat/src/com/ibm/ws/jpa/tests/beanvalidation/tests')
      into new File('fat/src/com/ibm/ws/jpa/tests/beanvalidation/tests')
    }
}

task copyCommonFiles {
  dependsOn ':com.ibm.ws.jpa.tests.beanvalidation_fat.common:build'
  doLast {
    /*
     * Copy test application resources.
     */
    copy {
      from project(':com.ibm.ws.jpa.tests.beanvalidation_fat.common').file('test-applications')
      into new File('test-applications')
    }
    
    /*
     * Copy common publish files.
     */
    copy {
      from project(':com.ibm.ws.jpa.tests.beanvalidation_fat.common').file('publish')
      into new File('publish')
    }
  }
}

addRequiredLibraries {
  dependsOn copyJdbcDrivers
  dependsOn addhibernateJPA22
  dependsOn addOpenJPA_JPA22
  dependsOn addJakartaTransformer
  dependsOn addJPAFATTools
  dependsOn copyFAT
  dependsOn copyTestContainers
}

jar {
  dependsOn copyCommonFiles
}

autoFVT.dependsOn copyCommonFiles

clean.doLast {
  if (file('test-applications').exists()) {
    file('test-applications').deleteDir()
  }
  if (file('build').exists()) {
    file('build').deleteDir()
  }
  if (file('publish/servers').exists()) {
    file('publish/servers').deleteDir()
  }
  if (file('fat/src/com/ibm/ws/jpa/tests/beanvalidation/tests').exists()) {
    file('fat/src/com/ibm/ws/jpa/tests/beanvalidation/tests').deleteDir()
  }
}
