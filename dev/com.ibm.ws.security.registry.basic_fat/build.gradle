/*******************************************************************************
 * Copyright (c) 2018, 2024 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License 2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-2.0/
 * 
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/

/* 
 * This is where all application ZIP and WARs will be built.
 */
def appBuildDir = "${buildDir}/test-application"

dependencies { 
  requiredLibs project(':io.openliberty.org.apache.commons.codec'),
    'org.apache.httpcomponents:httpclient:4.1.2',
    'org.apache.httpcomponents:httpcore:4.1.2',
    project(':com.ibm.ws.webcontainer.security_test.servlets'),
    project(':com.ibm.ws.security.registry_test.servlet')
}

autoFVT.dependsOn ':com.ibm.ws.security.registry_test.servlet:assemble'
autoFVT.dependsOn ':com.ibm.ws.webcontainer.security_test.servlets:assemble'

autoFVT.doLast {
  /*
   * Copy the clientcert.zip into test servers.
   */
  def servers = [
    'com.ibm.ws.security.registry.basic.fat.custom.certmapper.bell',
    'com.ibm.ws.security.registry.basic.fat.custom.certmapper.feature'
  ]
  servers.each { server ->
    copy { 
      from new File(project(':com.ibm.ws.webcontainer.security_test.servlets').buildDir, 'test-application/clientcert.zip')
      into new File(autoFvtDir, 'publish/servers/' + server + '/apps')
      rename 'clientcert.zip', 'clientcert.war'
    }
  }
  
  /*
   * Copy the userRegistry WAR into test servers.
   */
  servers = [
    'com.ibm.ws.security.registry.basic.fat',
    'com.ibm.ws.security.registry.basic.fat.federated',
    'com.ibm.ws.security.registry.basic.fat.ignorecase'
  ]
  servers.each { server ->
    copy { 
      from new File(project(':com.ibm.ws.security.registry_test.servlet').buildDir, 'test-application/userRegistry.war')
      into new File(autoFvtDir, 'publish/servers/' + server + '/dropins')
    }
  }
  
  /*
   * Copy the certificate mapper JAR to both the shared directory for use as a BELL and
   * to the bundles directory for use as a user feature.
   */
   copy { 
     from new File(appBuildDir, 'com.ibm.ws.security.registry.basic.certificate.mapper.sample_1.0.jar')
     into new File(autoFvtDir, 'publish/shared')
   }
   copy { 
     from new File(appBuildDir, 'com.ibm.ws.security.registry.basic.certificate.mapper.sample_1.0.jar')
     into new File(autoFvtDir, 'publish/bundles')
   }
}

/*
 * Generate the JAR for the certificate mapper sample. This JAR will be compatible
 * for use both as a BELL and as a user feature.
 */
task certificateMapper_JAR(type: Jar, dependsOn: classes) { 
  destinationDirectory = file("${appBuildDir}")
  archiveFileName = "com.ibm.ws.security.registry.basic.certificate.mapper.sample_1.0.jar"
  manifest { 
  	from 'test-applications/certificateMappers/META-INF/MANIFEST.MF'
  }

  from ('build/classes/java/main') { 
    include 'test/cert/mappers/*.class'
  }
  from ('test-applications/certificateMappers') {
    include 'META-INF/services/*'
    include 'OSGI-INF/**'
  }
}

assemble { 
  dependsOn \
  certificateMapper_JAR
}
