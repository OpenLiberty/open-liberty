/*******************************************************************************
 * Copyright (c) 2017 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/
/*
 * Master Gradle build script
 *
 * Depends on bnd_* values from gradle.properties.
 */

ext {
  cnf = rootProject.project(bnd_cnf)
  buildImage = project(':build.image')
}

subprojects {
  plugins.apply 'java'
  def bndProject = bndWorkspace.getProject(name)
  if (bndProject != null) {
    plugins.apply 'biz.aQute.bnd'
  } else {
    task publish { }
    task release { }
    task releaseNeeded { dependsOn release }
  }

  /* For Java 9 builds, set the --release compiler flag to keep bootclasspath in sync */
  tasks.withType(JavaCompile) {
    doFirst {
      if(JavaVersion.current().isJava9Compatible()) {
        def release = sourceCompatibility
        if(sourceCompatibility < '9')
          release = sourceCompatibility.substring(2);
        println "Compiling with --release=" + release
        options.compilerArgs.addAll([ '--release', release])
      }
    }
  }

  if (bndProject != null) {
    apply from: cnf.file('gradle/scripts/repos.gradle')
    apply from: cnf.file('gradle/scripts/publish.gradle')
    apply from: cnf.file('gradle/scripts/tasks.gradle')

    if (bndis('fat.project')) {
      apply from: cnf.file('gradle/scripts/fat.gradle')
    }
  }

  // Make the release task depend on assemble so that invoking 'release' or 'releaseNeeded'
  // will publish jar updates to the local build.image/wlp image
  release.dependsOn assemble
  release.dependsOn publish
}

task testResults {
  dependsOn subprojects.test

  doLast {
    generatedProps.setProperty('tests.total.failed', failedTestCountTotal.toString())
    generatedProps.setProperty('tests.total.successful', successfulTestCountTotal.toString())

    File propsFile = new File('generated.properties')
    generatedProps.store(propsFile.newWriter(), null)
  }
}

task wrapper(type: Wrapper) {
  jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
}

